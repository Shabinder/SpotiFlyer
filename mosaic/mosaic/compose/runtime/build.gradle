import org.jetbrains.kotlin.gradle.plugin.KotlinPluginKt

apply plugin: 'org.jetbrains.kotlin.jvm'
apply plugin: 'com.vanniktech.maven.publish'

archivesBaseName = 'mosaic-compose'

sourceSets {
	common {
		kotlin {
			srcDir '../upstream/compose/runtime/runtime/src/commonMain/kotlin'
		}
	}
	main {
		kotlin {
			srcDir '../upstream/compose/runtime/runtime/src/commonMain/kotlin'
			srcDir '../upstream/compose/runtime/runtime/src/jvmMain/kotlin'
			srcDir '../upstream/compose/runtime/runtime/src/desktopMain/kotlin'
		}
	}
	test {
		kotlin {
			srcDir '../upstream/compose/runtime/runtime/src/test/kotlin'
		}
	}
}

dependencies {
	api deps.kotlinxCoroutinesCore

	add(KotlinPluginKt.PLUGIN_CLASSPATH_CONFIGURATION_NAME, project(':compose:compiler'))

	testImplementation 'org.jetbrains.kotlin:kotlin-test'
	testImplementation deps.kotlinxCoroutinesTest
}

tasks.withType(org.jetbrains.kotlin.gradle.tasks.KotlinCompile).configureEach {
	kotlinOptions.freeCompilerArgs += [
		'-Xmulti-platform',
		'-Xcommon-sources=' + sourceSets.common.kotlin.files.join(","),
	]
}
