import com.github.jengelman.gradle.plugins.shadow.tasks.ShadowJar

apply plugin: 'java-library'
apply plugin: 'com.vanniktech.maven.publish'

dependencies {
	compileOnly project(':compose:compiler-hosted')
}

TaskProvider<ShadowJar> shadowJar = tasks.register("embeddedPlugin", ShadowJar) {
	configurations = [project.configurations.compileClasspath]
	relocate("com.intellij", "org.jetbrains.kotlin.com.intellij")
	archiveBaseName.set("embedded")
	archiveVersion.set("")
	destinationDirectory.set(new File(buildDir, "repackaged"))
}

configurations {
	// Replace the standard jar with the one built by 'shadowJar' in both api and runtime variants.
	apiElements.outgoing.artifacts.clear()
	apiElements.outgoing.artifact(shadowJar.flatMap {it.archiveFile })
	runtimeElements.outgoing.artifacts.clear()
	runtimeElements.outgoing.artifact(shadowJar.flatMap {it.archiveFile })
}
